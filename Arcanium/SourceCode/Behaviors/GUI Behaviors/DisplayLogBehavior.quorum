use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Game.Game
use Libraries.Containers.Array
use Libraries.Interface.Item2D
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Color
use Libraries.Game.Graphics.Label
use Libraries.Interface.Controls.Button
use Libraries.Game.Graphics.Camera

class DisplayLogBehavior is Behavior
    
    Game game
    Array<Item2D> removalArray
    Array<text> log
    Array<Button> buttons
    Array<Label> labels
    Drawable displayBox = undefined
    Color color
    boolean finished = false
    Camera camera = undefined
    
    action Run(BehaviorEvent event)
        if log:GetSize() = 0
            finished = true
        end

        if displayBox = undefined
            Drawable newBox
            displayBox = newBox
            displayBox:LoadFilledRectangle(800, 180, color:LightGray())
            displayBox:SetPosition(camera:GetPosition():GetX()-camera:GetWidth()/2, camera:GetPosition():GetY()-camera:GetHeight()/2)
            game:Add(displayBox)
        end
        
        integer counter = 0
        
        repeat while counter < log:GetSize()
            Label label
            label:SetText(log:Get(counter))
            label:SetFontSize(20)
            label:SetPosition(-1000, -1000)
            label:SetHorizontalMargins(5)
            label:SetFontSize(22)
            label:SetMaximumWidth(camera:GetWidth())
            labels:Add(label)
            displayBox:Add(label)

            Button button
            NextLogBehavior behavior
            behavior:SetLabels(labels)
            behavior:SetButtons(buttons)
            behavior:SetCurrentIndex(counter)
            behavior:SetLogBehavior(me)
            behavior:SetDisplayBox(displayBox)
            behavior:SetGame(game)
            button:SetBehavior(behavior)
            button:SetName(log:Get(counter))
            button:SetPosition(-1000, -1000)
            buttons:Add(button)
            game:Add(button)
            
            counter = counter + 1
        end

        if buttons:GetSize() > 0
            game:SetFocus(buttons:Get(0))
        end

        if labels:GetSize() > 0
            labels:Get(0):SetPosition(10, displayBox:GetHeight() - 30)
        end
    end
    
    action Dispose
        integer counter = 0

        repeat while counter < buttons:GetSize()
            removalArray:Add(buttons:Get(counter))
            removalArray:Add(labels:Get(counter))
            removalArray:Add(displayBox)            
            counter = counter + 1
        end
    end

    action IsFinished returns boolean
        return finished
    end

    action SetFinished(boolean bool)
        finished = bool
    end
    
    action SetGame(Game newGame)
        game = newGame
    end
    
    action SetRemovalArray(Array<Item2D> array)
        removalArray = array
    end

    action SetDisplayBox(Drawable item)
        displayBox = item
    end

    action SetCamera(Camera gameCamera)
        camera = gameCamera
    end

    action SetLog(Array<text> newLog)
//        log = newLog
        integer counter = 0

        repeat while counter < newLog:GetSize()
            log:Add(newLog:Get(counter))

            counter = counter + 1
        end
    end
end