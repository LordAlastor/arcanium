use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Containers.Array
use Libraries.Game.Game
use Libraries.Interface.Item2D
use Libraries.Game.Graphics.Label
use Libraries.Game.Graphics.Drawable
use Libraries.Game.Graphics.Camera

class CombatUpdateStatusBehavior is Behavior
    
    Game game
    Battle battle
    Array<Item2D> removalArray
    Camera camera = undefined
    BehaviorQueue behaviorQueue
    Array<Combatant> combatants
    Label playerMana
    Array<Label> statusNames
    Array<Label> statusInfo
    Array<Drawable> statusWindows
    Array<Drawable> sprites
    boolean finished = false
    Player player

    action Run(BehaviorEvent event)

        player = cast(Player, combatants:Get(0))
        integer counter = 0

        playerMana:SetText("Mana: " + player:GetCurrentMP() + "/" + player:GetMaxMP())

        repeat while counter < statusWindows:GetSize()
            if (combatants:Get(counter) not= undefined) and (not combatants:Get(counter):CheckDefeated())
                statusInfo:Get(counter):SetText("Health: " + combatants:Get(counter):GetCurrentHP() + "/" + combatants:Get(counter):GetMaxHP())
            elseif (combatants:Get(counter) not= undefined)
                if counter = 0
                    removalArray:Add(playerMana)
                else
                    player:AddExperience(combatants:Get(counter):GetExperienceValue(), combatants:Get(counter):GetLevel())
                end

                combatants:Set(counter, undefined)

                removalArray:Add(statusNames:Get(counter))
                statusNames:Set(counter, undefined)

                removalArray:Add(statusWindows:Get(counter))
                statusWindows:Set(counter, undefined)

                removalArray:Add(statusInfo:Get(counter))
                statusInfo:Set(counter, undefined)

                removalArray:Add(sprites:Get(counter))
                sprites:Set(counter, undefined)
            end

            counter = counter + 1
        end

        CheckCombatOver()
        finished = true
    end

    action CheckCombatOver
        integer counter = 1
        boolean enemiesRemain = false

        repeat while counter < combatants:GetSize()
            if combatants:Get(counter) not= undefined
                enemiesRemain = true
            end

            counter = counter + 1
        end

        if combatants:Get(0) = undefined
            behaviorQueue:StopBehaviors()

            Array<text> gameOver
            gameOver:Add(player:GetName() + " was defeated! Game over!")

            DisplayLogBehavior gameOverMessage
            gameOverMessage:SetGame(game)
            gameOverMessage:SetLog(gameOver)
            gameOverMessage:SetRemovalArray(removalArray)
            gameOverMessage:SetCamera(camera)
            behaviorQueue:AddBehavior(gameOverMessage)

//            need behavior to end game after displaying Game Over text
        elseif not enemiesRemain
            battle:EndCombat()
        end
    end

    action IsFinished returns boolean
        return finished
    end

    action SetGame(Game newGame)
        game = newGame
    end

    action SetBattle(Battle newBattle)
        battle = newBattle
    end

    action SetRemovalArray(Array<Item2D> newArray)
        removalArray = newArray
    end

    action SetBehaviorQueue(BehaviorQueue newQueue)
        behaviorQueue = newQueue
    end

    action SetCamera(Camera newCamera)
        camera = newCamera
    end

    action SetCombatants(Array<Combatant> newArray)
        combatants = newArray
    end

    action SetPlayerMana(Label newLabel)
        playerMana = newLabel
    end

    action SetStatusNames(Array<Label> newArray)
        statusNames = newArray
    end

    action SetStatusInfo(Array<Label> newArray)
        statusInfo = newArray
    end

    action SetStatusWindows(Array<Drawable> newArray)
        statusWindows = newArray
    end

    action SetSprites(Array<Drawable> newArray)
        sprites = newArray
    end
end